шифр 419999 зсх^
лен 42(2099)^
лен 43(2099-зп)^
еев1а3
*name e64.b6
*bemsh
ВВД$$$
Э64 СТАРТ '10000'
 Е
 М
 Б
 УПОТР А(М7)
 КОНД В'0'          ИПЗ АЦПУ
 КОНД М41В'3'
 КОНД (21)В'0'
 КОНД В'0'  НЕ ЗАНИМАТЬ
БР8С СЧИ М12
 СДА 64-15
 НТЖ 3(М10)
 И Е48П16
 НТЖ 3(М10)
 ЗП 3(М10)
 ПБ БР6
 пам 3
 ПАМ 1     ПРИП
 КОНД В'0'
 КОНД М47В'1'
ЛИ СЧ (1)
 ПО (5)           ЛИСТОВАНИЯ НЕТ
 СЛЦ Е1
 ПБ ЛИ1Б
 ПБ (М5)
СМ ПАМ 2
СМ11 ПАМ 1
 ЗП СМ            УПРЯТЫВАНИЕ СУММАТОРА
 ПБ А2
ЛИ1Б ЗП Л(М1)
ЛИ1В СЧ Е40П1
 И ЧС(М1)
 ЗП ЧС(М1)
 ПБ (М5)
БР13 СЧ ЗВОН
 ПЕ БР5А
 МОДА МЕТКА     ПОВТОР
 ПВ (М11)
 УИ М11
 СЧ МАТР-7
 ЗП СИМВ
 НТЖ П177
 ПЕ БР13Б         ПРЕДЫД. СИМВ НЕ СТРЕЛКА
 СЧИ М11
 И П17
 ЗП ОСТ+2
БР1 МОДА БР20А
 УИА (М11)
 ПБ КОНСТР
БР13Б МОДА БР13В
 УИА (М5)
БР13В МОДА БР2
 ПИО (М11)
 СЛИА -1(М11)
 ПБ БР3
БР11 МОДА МЕТКА   ПОВТОР
 ПВ (М11)
 СДА 64+7
 ПЕ БР1
 СЧ СИМВ
 ВЧ МАТР-4
 ПО БР11Б
 СЧ
 ЗП ОСТ+2   ПРОТ
 МОДА КОНСТР
 ПВ (М5)
 СЧ СИМВ
 ПЕ БР11Б         НОМ. ПОЗИЦ.#0
 СЧ Е8
 МОДА БР68
 ПВ (М15)
БР11Б СЧ СИМВ
 ЗП МАТР-4
БР2 МОДА БР20А
 УИА (М11)
 ПБ МЕТКА
БР8 СЧ П177          БЛОК ОБРАБ. СТРЕЛ. ВНИЗ
 ЗП МАТР-7
 ПБ КОНСТР
БР20 ЗП СИМВ
 ПБ БР22
БР5А СЧ П17
 ЗП СИМВ
БР3 СЧ СИМВ
 ЗП МАТР-7
ББ НТЖ П17
 ПО БР5
БР22 СЧ МАТР-4
 СДА 64+2
 УИ М16
 СЛИ М16(М7)
 СЧ МАТР-4
 И П3
 СДА 64-3
 ВЧОБ
 УИ М15
 СЧ СИМВ
 СЛЦ Е1
 СДА 64(М15)
 ИЛИ МАТР+1-А(М16)
 ЗП МАТР+1-А(М16)
БР5 СЧ МАТР-4
 СЛЦ Е1
 ЗП МАТР-4
 СДА 64+7
 ПО (М5)
 СЧ ЗВОН
 ПО М172
ЕСК УИА '172'(М15)
 СЧИ М15
 ЗП СИМВ
 ПБ БР20А
БР17А СЧИ М10
 ЗП СИМВ
 СЧИ М6
 СЛЦ СИМВ
 ЗП СИМВ
 СЛИ М10(М6)
 СЧ МАТР-1
 ПО БР175
 ВЧ Е1
 ЗП МАТР-1
 СЛИА 1(М10)
 СЧ СИМВ
 СЛЦ Е1
 ЗП СИМВ
БР172 СЧ СИМВ
 ВЧОБ Б5+2
 ПО БР17В         КОНЕЦ СТРОКИ
 СЧ ОСТ+1
 ВЧ Е1
 ЗП ОСТ+1
 РЖА 7
 ПО БР172А        СТРОКА ПОСЛЕДНЯЯ
 СЧ ОСТ
 ПБ БР172Б
БР172А СЧ ОСТ
 ПО БР171         КОНЕЦ ЭКСТРАК.
 ВЧ Е1
БР172Б ЗП МАТР-1
 СЧ Б5+5
 СЛЦ Е1
 ЗП Б5+5
 УИ М10
 ПБ БР174
М172 СЧИ М10
 НТЖ Б5+2
 ПЕ КОНСТР
 МОДА КОНСТР
 ПИО (М13)
 СЧ ОСТ+9
 ПО КЛ
 НТЖ Е3
 ПЕ КОНСТР
КЛ СЧИ М11
 ЗП СМ11
 СЧИ М5
 ЗП СМ5
 МОДА МЕТКА
 ПВ (М11)
 МОД СМ5
 УИА (М5)
 МОД СМ11
 УИА (М11)
 СЛИА -1(М13)
 СЛИА -122(М15)
 МОДА ЕСК
 ПИО (М15)
 СЛИА -31(М15)
 МОДА ЕСК
 ПИО (М15)
 ПБ КОНСТР
БР17 МОДА БР17А
 ПИНО (М6)          ЕСЛИ ВЫДАЧА В СТОЛБЦЫ
 СЧИ М10
 ВЧ Б5+2
 СЛИА 1(М10)
 ПО БР171         КОНЕЦ ЭКСТРАКОДА
БР17В СЧ ОСТ+10
 ПЕ И9
 СЧ ОСТ+12
 ПО И10
БР174 СЧ Б5+10
 УИ М3
 МОДА И10
 УИА (М5)
 СЧ МАТР-4
 ПО (М5)
КОНСТР СЧ ОСТ+2
 СЛЦ ЧС(М1)
 ПЕ КОН1          КОНЕЦ ЛИСТА
КОН СЧ ПРОТ(М1)
 И П377
 ПО КОНС1
 СЧ МАТР+1
 СДА 64+8
 И П377
 НТЖ П213
 ПЕ КОНС
 СЧ Е48П41
 И ЧС(М1)
 ЗП ЧС(М1)
 СЧ МАТР+1
 И О16П9
 ИЛИ Е13
 ЗП МАТР+1
КОНС СЧ ПРОТ(М1)
 И П377
 СЛЦ П141
 МОДА БР68
 ПВ (М15)
 СЧИ М17
 И П377
 ПО УПРАБЗ
КОНС1 СЧ ОСТ+2
 СДА 64-40
 СЛЦ ЧС(М1)
 ЗП ЧС(М1)
 ПО КОН5
 СЧ Л(М1)
 СДА 64+1
 ПО КОН5
 СЧ Е1
 ЗП ОСТ+2
КОН5 УИА (М12)
 УИА (М4)
 УИА -2(М16)
 МОДА МАТР
 УИА (М2)
 СЧ 1(М2)
 И П377
 ПБ ВЫБ3Б
КОН1 МОД ИПЗ
 СЧ ЯЧКОПИ
 СДА 64+45
 УИА '110'(М12)
 ПО КОП
 УМН ЧСТ
 СЧМР 64
 УИ М12
КОП СЧИ М12
 СЛЦ 32(М7)
 ЗП ОСТ+15
 И Е16
 УИА 39(М12)
 ПО БР82          МЕТРЫ КОНЧИЛИСЬ
 СЧ ОСТ+15
 ЗП 32(М7)
 СЧ Е40П1
 И ЧС(М1)
 ИЛИ =М42В'56'
 ЗП ЧС(М1)
 СЧ Л(М1)
 СДА 64+1
 ПО КОН           НЕТ ЛИСТОВАНИЯ
 СЧ =В'176'
 МОДА БР68
 ПВ (М15)
 СЧ ДЛИНА
 ЗП ЧС(М1)
 пб кон
КОН4 СЧ 2(1)
 СЛЦ Е1
 ЗП 2(1)
 ПВ ПЕРЕВ(М15)
 ИЛИ =М32Х'8080'М24В'360'
 ЗП 6(1)
 УИА 3
 УИА -4(М16)
КОН3 МОДА (М16)
 СЧ 7(1)
 СЛЦ =П'111111'
 ЗП (М17)
 МОДА КОН3
 ЦИКЛ (М16)
 СЧ Е40П1
 И ЧС(М1)
 ИЛИ =М39В'574'
 ЗП ЧС(М1)
 ПБ КОН
ПЕРЕВ ВНЕШН дисп99.ПЕРЕВ
КОН2 СЧ П174   КНЦ АБЗ
 МОДА БР68
 ПВ (М15)
 СЧ
 МОДА БР67
 ЦИКЛ (М14)
 СЧИ М17
 ИЛИ П377
 СЛЦ Е1
 УИ М17
 И П1777
 СЛИА 2(М17)
 ПЕ КОН4          ЕСТЬ АБЗАЦЫ
 СЧ ОСТ+3
 УИ М14
 СЧ НУЛЯЧ
 ИЛИ (М14)
 ЗП (М14)
 СЧ ОСТ+7
 ПВ ФИЗОБМ(М16)
 МОДА И9А
 УИА (М15)
 МОДА БР171А
 ПВ (М12)
 УИА 3
 ПБ КОН4
БР82 МОДА БР8С
 УИА (М3)
БР82А СЧ ИПЗ
 УИ М10
 МОДА Ю5
 ПИО (М5)          ЕСЛИ ЕСТЬ ОШИБКА В ИНФОРМ.
 СЧ СМ
 И ЕЗП
 УИА '2003'
 ПО БР82Б
БР60Б СЧ 32(М10)
 И Е48П17
 ИЛИ 32(М7)
 ЗП 32(М10)       ЧИСЛО СТРОК
БР60А СЧИ М17           УПРЯТ.РЕГИСТРОВ
 И П1777
 ЗП Б5+6
 СЧИ М14
 СДА 64-15
 ИЛИ Б5+6
 МОД ОСТ+3
 ЗП
 МОД ОСТ+3
 УИА (М14)
 СЧ НУЛЯЧ
 ИЛИ (М14)
 ЗП (М14)
 МОДА УПР1
 ПИНО (М5)          НЕ КЗ
УПР2 СЧ ОСТ+7
 ПВ ФИЗОБМ(М16)   ЗАПИСЬ АБЗАЦА НА МВ
 МОДА БР171А
 ПВ (М15)         ПЕР. ТРАКТА НА ЛЕНТУ
Ю5Б НОП 0
 СЧ ОСТ+7
 СДА 64+2
 И Е15П11
 УИ М15
 СЧ 0
 ЗП (М15)
Ю5А СЧ Б5
 СДА 64-42
 ИЛИ ОСТ+7
 ПВ ТБУФ(М15)
Ю5 УИА 3
 ПБ (3)
БР70 УИА -31(М2)
 СЧ Е48П41
 И ЧС(М1)
 ИЛИ ОСТ+2
 ЗП ПРОТ(М1)
 СЧ
БР70А1 МОДА МАТР1-3
 ЗП (М2)
 МОДА БР70А1
 ЦИКЛ (М2)
 ЗП МАТР-4
 СЧ Е1
 ЗП ОСТ+2
 ПБ (М5)
ВЫБ9 СЧИ М12
 СЛЦ П177
Ф МОДА БР68
 ПВ (М15)         ЗАНЕСЕН. НОМЕРА ПОЗИЦ.В АБЗАЦ
ВЫБ3А СЧ Б5+9
 УИА -1(М16)
ВЫБ3Б ЗП ОСТ+14
 ПО ВЫБ3          ЕСЛИ БЫЛ ПРОБЕЛ
 МОДА БР68
 ПВ (М15)         ЗАНЕС. СИМВ. В АБЗАЦ
ВЫБ3 СЛИА 1(М16)
 МОДА БР63
 ЦИКЛ (М4)          ЕСЛИ ЯЧЕЙКА ВЫБРАНА НЕ ВСЯ
 УИА -3(М4)
 СЛИА 1(М2)
 СЧИ М12
 ВЧОБ МАТР-4
 ПЕ БР70
 СЧ (М2)
 ПБ БР63А
БР63 СЧ (М2)
 СДА 64+8
БР63А ЗП (М2)
 И П377          ВЫДЕЛ. СИМВОЛА
 ЗП Б5+9
 НТЖ ОСТ+14
 СЛИА 1(М12)
 ПО ВЫБ3          ЕСЛИ НУЖНЫЙ СИМВОЛ
 СЧ ОСТ+14
 ПО ВЫБ9          ЭТО БЫЛ ПРОБЕЛ
 СЧИ М16
 ПО ВЫХОД
 НТЖ Е1
 ПЕ ВЫБ10         ЕСЛИ ЕСТЬ ЧИСЛО ПОВТОРЕНИЙ
 СЧ ОСТ+14
 ПБ ВЫБ10А
ВЫБ10 СЧ П177
 МОДА БР68
 ПВ (М15)         ЗАНЕС. ПРИЗН. ЧИСЛА ПОВТОР. В АБЗАЦ
 СЧИ М16           ЧИСЛО ПОВТОРЕНИЙ
ВЫБ10А МОДА ВЫХОД
 УИА (М15)
БР68 МОДА БР67          ЗАНЕСЕН. СИМВ. В АБЗАЦ
 ЦИКЛ (М14)
 СЛИА 1(М17)
 УИА -5(М14)
БР67 ЗП ОСТ+15
 СЧ -1(М17)
 СДА 64-8
 ИЛИ ОСТ+15
 ЗП -1(М17)
 ПБ (М15)         ВЫХОД ИЗ БЛОКА.
ВЫХОД СЧИ М17
 И П377
 ПЕ ВЫБ3А         АБЗАЦ НЕ ЗАПОЛНЕН
 СЧИ М12
УПРАБЗ ЗП МАТР-8
 СЧ П174   КНЦ АБ
 МОДА БР68
 ПВ (М15)         ЗАНЕС. ПРИЗН. КОНЦА СТРОКИ
 СЧ
 МОДА БР67
 ЦИКЛ (М14)
 СЧИ М17
 СЛИА 2(М17)
 И П1777
 ПЕ УПР           ТРАКТ НЕ ЗАПОЛНЕН
 СЧ ОСТ+3
 УИ М14
 СЧ НУЛЯЧ
 ИЛИ (М14)
 ЗП (М14)
 СЧ ОСТ+7
 ПВ ФИЗОБМ(М16)
 МОДА И9А
 УИА (М15)
 МОДА БР171А        ЗАПИСЬ ТРАКТА НА МЛ
 ПВ (М12)
УПР СЧ МАТР-8
 ПО КОНС1
 УИ М12           НОМЕР ПОЗИЦИИ
 СЛЦ П177
 ИЛИ Е8
 ПБ Ф
ПОЛ МОД СМ
 СЧ Е48-1-ПЗ
 МОДА НОВ1А
 УИА (М15)
 ПБ ВШГП
БР175 СЧ ОСТ+1
 ПЕ БР172
БР171 СЧ МАТР-4
 УИ М3
 СЧ ЗВОН
 ПО МК
 СЧ Б5+7
 ПБ Б171А
МК СЧ Б5+7
 МОДА Б171А
 ПИО (М3)
 СЛЦ Е1
Б171А ЗП ОСТ+2
 МОДА КОНСТР        НА КОНЕЦ СТРОКИ
 ПВ (М5)
 МОДА БР82А
 ПВ (М3)
БР6 СЧ 33(М7)        ВЫХОД
 И =М23В'-1'М47В'1'
 СЛЦ 33(М10)
 ЗП 33(М10)
 СЧ П3
 СЛЦ ЖДЕТ(М10)
 ЗП ЖДЕТ(М10)
 СЧ СХ
 ПО НЕПР
 ЗП Л(М1)
НЕПР СЧ СМ
 И ЕЗП
 ПЕ ПОЛ
 МОД СМ
 СЧ Е48-1
 ПВ ВШГ(М15)
НОВ1А ноп
 ПБ висп16
Б8 СЧ МАТР-4
 СЛЦ Е1
 ЗП МАТР-4
 ПБ БР8
БР82Б СЧ 33(М7)
 И Е3
 ПО БР60В         НЕ КОНЕЦ ЗАДАЧИ
 СЧ 0
 ЗП Л(М1)
 ЗП ЧС(М1)
 УИА (М5)
 СЧ =В'175'
 МОДА БР68
 ПВ (М15)
 ПБ БР60А
БР171А ноп
 мод ипз
 сч шзнмб+2
 слц е25
 мод ипз
 зп шзнмб+2
 пб (м15)
БР60В СЧ 32(М7)
 И П77777
 СЛЦ 32(М10)
 ЗП 32(М7)
 И Е16
 ПЕ БР60Б
 УИА 39(М12)       ДАЙ МЕТРЫ АЦПУ
 МОДА БР8С
 УИА (М3)
 ПБ БР60А
АЛГОЛ УИИ М14(М10)
 СЛИА 21(М14)
 СЧИ М14
 ЗП Б5+2   А КОН
 СЧ Е48
 ЗП ЗВОН
 УИА 0
 СЧ 1(М3)
 УИА 3
 ЗП Б5+3
 И Е24
 ПО ПЛ
 СЧ Б5+3
 СДА 64+44
 ПО И8В
 НТЖ Е3
 ПО И8В
 СЧ 0
ПЛ ЗП ЗВОН
 ПБ И8В
ИТМ И Е47
 НТЖ Е47
 ЗП СИМВ+1
 СЧ Б5+3
 И П177
 ПБ АИТМ
АЛГОЛ1 ПО АЛГ
 СЧ Б5+2
 УИ М12
 УИА '2000'
 СЧ (М12)
 УИА 3
 ЗП Б5+3
 СДА 64+24
 И П17
 ЗП Б5+7   ПРОТЯЖ
АЛГ СЧ Б5+8
 ПБ АЛГ1
ЛИСТ СЧ Л(М1)
 ИЛИ Е1
 ЗП Л(М1)
 ПБ ЛИ1А
УПР1 СЧ 36(М10)
 И Е41
 ПО Ю5А
 НТЖ 36(М10)
 ЗП 36(М10)
 СЧ
 ЗП 32(М7)
 ПБ УПР2
 ЛИТЕР
ДЛИНА КОНД М39В'562'
П141 КОНД В'141'
НУЛЯЧ КОНД В'0'
*
А2 НОП ,
 ЗП Б5
 СЧ 0
 ЗП ЗВОН
 ЗП СХ
 МОД Б5
 СЧ ТУС-1
 УИА (М5)
 УИ М2            АДРЕС ИПЗ МАТЕМ.
 ЗП ИПЗ
 СЧ Е48
 ЗП 33(М7)
 СЧ 32(М2)
 ИЛИ Е48П17
 НТЖ Е48П17
 ЗП 32(М7)
 сч 3(м2)
 и п77777
 зп 3(м2)       номер авоста
*ВЫДЕЛЕНИЕ АН. И АК. ВЫД. ИНФОРМ.
 СЧ СМ
 И ЕЗП
 ПО Э64ОС         Э64 ОТ ОС
 СЧ 33(М2)
 И Е23
 ПО ИКАН          НЕ БЫЛО 'ДАЙ МЕТРЫ АЦПУ'
 УИА 39(М12)
 УИИ М10(М2)
 ПБ БР8С          НА АВРАСП
Э64ОС НОП ,
 СЧ 33(М2)
 И Е3
 ИЛИ 33(М7)
 ЗП 33(М7)
 СЧ ИЗАХВ(М2)
 ИЛИ ЕЗП
 ЗП Б5            НОМЕР ЗАДАЧИ МАТЕМАТ.
 МОД Б5
 СЧ ТУС-1
 ЗП ИПЗ
ИКАН НОП
 МОД ИПЗ
 СЧ АВОС
 И Е31
 ЗП НУЛЯЧ
 СЧ Б5
 СДА 64-1
 УИ 1
 МОДА Б1-128
 СЛИА (М1)
 УИА (М6)
 УИА 3
 РЖА 3
И10Б УИА 57(М12)
 СЧ 36(М2)        ВЫДЕЛ. АДРЕСА ИНФ. СЛОВА
 УИ М14
 ЗП Б5+10
 УИА 1(М13)
 МОДА БР6
 УИА (М3)
 И П77777
 ЗП ОТЛ
 ПО ЧАСТЬ         ЧАСТИЧНАЯ ВЫДАЧА
 ВЧОБ Е1
 ПО ЛИСТ
 МОДА И40
 ПВ (М17)         ВЫБОРКА ИНФ. СЛОВА
 ЗП Б5+8
 АIF (&ЭФ ЕQ 0).ПРОД1
 И Е19
 НТЖ Е19
 ИЛИ КЛЮЧЛ
 ЗП КЛЮЧЛ
 ПО ПБУФ
ПРОД СЧ Б5+8
.ПРОД1 АNОР
 УИИ М3(М14)
И73 УИ М10
 СДА 64+20
 И П17           ВЫДЕЛ. ИР.
 ПО И71           ЕСЛИ РЕГИСТРА НЕТ
 ВЧОБ
 УИ М11
 СЛИ М11(М2)
 СЧ 22(М11)       СОДЕРЖ. ИР
 УИ М11
 СЛИ М10(М11)      АН. И АК
И71 МОДА И72
 ПИО (М13)
 СЧИ М10
 ЗП Б5+2          ЭТО АДРЕС КОНЦА
 УИА (М13)
 СЧ Б5+3
 СДА 64+24
 ПБ И73           НА ВЫДЕЛ. АДРЕСА НАЧАЛА
ЛИСТ1 сч 0
 ЗП Л(М1)
ЛИ1А УИИ М10(М2)
 ПБ Ю5
ЧАСТЬ СЧ СМ
 И ЕЗП
 ПЕ ЛИСТ1
 СЧ ИПЗ
 УИ М10
 СЧ 11(М2)
 УИ М12
 СДА 64+6
 ПО НЕУПР         ДЛЯ УПРЯЧЬ АДРЕС > 77
 СЧ (М12)
 НТЖ УПРЯЧЬ
 ПЕ НЕУПР
 МОД 9(М2)
 УИА (М17)
 УИИ М16(М1)
 УИА -1(М15)
 СЧ 10(М2)
 ПЕ ЧТБУФ
 УИИ М16(М17)
 УИИ М17(М1)
ЧТБУФ СЧ (М16)
 ЗП (М17)
 СЛИА 1(М16)
 МОДА ЧТБУФ
 ЦИКЛ (М15)
 ПБ Ю5
НЕУПР СЧ ЗОНЫ(М10)
 И П1777
 ПО Ю5
 СЧ СМ
 ЗП ОСТ+8
 СЧ ЗАВР
 ЗП СМ
 МОДА ТИГР2
 ПВ (М12)
 СЧ ОСТ+8
 ЗП СМ
 ПБ Ю5Б
И72 СЛИА 1(М14)
 ПБ АЛОЗ
И74А СЧИ М10
 ЗП Б5+5          АДР. НАЧАЛА
 ВЧ Б5+2
 ПЕ И8В           ЕСЛИ ВЫДАЕТ. НЕ 1 ЯЧЕЙКА
 СЧИ М10
 ЗП Б5+2
 МОДА И40
 ПВ (М17)
 УИА (М4)
 УИИ М14(М10)
 И Е46Е45
 ПЕ И8В           НЕТ
 УИА 30(М12)       НОМЕР ТЕКСТА АВОСТА
 СЧ Б5+8
 СДА 64-24
 ПО АЛГОЛ
 НТЖ КОН22
 ПО АЛГОЛ
 НТЖ Е41
 ПО АЛГОЛ
 МОДА ВЫБ1
 УИА (М17)
 МОДА ВЫБСИМ
 ПВ (М11)
 МОДА БР82
 ПИО (М14)
И76 МОДА ВЫБСИМ
 ЦИКЛ (М4)
 ВЧ П140
 ПЕ ВЫБСИМ
 УИА -1(М4)
 СЧ О
И78 ЗП Б5+6          БЛОК СРАВН. НА СИМВ. ПОВТОР. И
 ПО И75
 И П377          НОМЕР ПОЗИЦИИ
 НТЖ СИМВ
 ПО ВЫБСИМ
 СЧ Б5+6
 СДА 64+8
 ПБ И78
И75 СЛИА -'172'(М15)
 МОДА И77
 ПИО (М15)
 СЛИА -'205'(М15)
 МОДА И77
 ПИО (М15)
 СЛИА '146'(М15)
 МОДА И76
 ПИНО (М15)         ЕСЛИ ПРИЗНАК НЕ НАЙДЕН
И77 СЛИА -1(М14)
 СЧИ М14
 ЗП Б5+2          АДРЕС КОНЦА ВЫДАВ. МАССИВА
 ПБ И8
И8В УИИ М14(М10)      ВЫДАВ. МАССИВ ПРИНАДЛ. МАТЕМ.
 УИА 58(М12)
 МОДА И40
 ПВ (М17)
 СЧИ М14
 СЛЦ П1777
 УИ М14
 ВЧ Б5+2
 ПЕ И40
 СЧ Б5+2
 УИ М14
 МОДА И40           АК. ПРИНАДЛ. МАТЕМАТ.
 ПВ (М17)
И8 УИИ М14(М3)
 УИА -45(М15)
 СЧ
И8А МОДА МАТР1+1       ЧИСТКА МАТРИЦЫ
 ЗП (М15)
 МОДА И8А
 ЦИКЛ (М15)
УИА УИА 57(М12)
 СЛИА 1(М14)
 МОДА И40
 ПВ (М17)
 И П177
 СЛЦ Е1
 СЛЦ МАТР-1        КОЛ-ВО ВЫДАВ. СТОЛБЦОВ
 ЗП МАТР-1
 УИА 34(М12)
 СДА 64+11
 ПЕ БР82
 СЧ Б5+3
 И Е24           ПОСЛЕДН. ИНФ. СЛОВО
 ПО УИА
 СЧ Б5+3
 СДА 64+20
 И П7
 ЗП Б5+7          ЧИСЛО ПРОТ. В КОНЦЕ ЭКСТРАК.
 СЧ ЗВОН
 ПБ АЛГОЛ1
АЛГ1 И Е44
 ПО И3            ЕСЛИ ВЫДАЧА В СТРОКИ
 СЧ Б5+8
 И Е41
 ПЕ И3
 СЧ Б5+8
 И Е20
 ПЕ И3
 СЧ Б5+2
 ВЧ Б5+5
 СЛЦ Е1            ДЛИНА МАССИВА
И4 ЗП ОСТ
 ВЧ МАТР-1
 СЛИА 1(М6)         ЧИСЛО СТРОК
 ПО И4
 СЧ ОСТ
 ЗП МАТР-1
 СЛИА -1(М6)
 СЧИ М6
 ЗП ОСТ+1         ЧИСЛО СТРОК В ВЫДАЧЕ
И3 СЧ Е1
 ЗП ОСТ+2         СТРОКА ДЛЯ ПРОТЯЖКИ
 МОДА И10
 УИА (М12)
ТИГР2 СЧ Б5
 И П77
 СДА 64-6
 ИЛИ Е6
 ПВ ТБУФ(М15)     ЗАПРОС АБЗАЦА
Т СДА 64-8
 ЗП ОСТ+3         АДРЕС АБЗАЦА
 УИ М14
 СДА 64-2
 СЛЦ НОММЛ3
 ЗП Б5+4
 УИА 1(М5)
 МОД ИПЗ
 СЧ ШИФРМ
 ЗП 1(М14)
 ЗП 257(М14)
 ЗП 513(М14)
 ЗП 769(М14)
 СЧ СМ
 НТЖ ЗАВР      АВРАСП
 ПО АВР
ТИГР МОД ИПЗ
 СЧ ШЗНМБ+2
 И Е34П25
 МОДА И9А           НА ТТРАКТ ЗА ЗОНОЙ
 УИА (М15)
 СЛЦ Б5+4
 ЗП ОСТ+7         КУС ЗП НА ДИСК
 АIF (&ЭФ ЕQ 0).ЗАН1
 СЧ КЛЮЧЛ
 ПО ПБУФ2
.ЗАН1 АNОР
 МОД ИПЗ
 СЧ ШИФРМ
М ЗП 1(М14)
 ЗП 257(М14)
 ЗП 513(М14)
 ЗП 769(М14)
 СЧ (М14)
 УИА 2(М17)
 ПО ТИГР3         НЕТ ВЫДАЧ
 УИ М17
ТИГР3 СЛИ М17(М14)
 СДА 64+15
 УИ М14
 СЧ
 МОД ОСТ+3
 ЗП
 ПБ (М12)
 АIF (&ЭФ ЕQ 0).ПБУФ1
ПБУФ СЧ Б5+8 1-ОЕ ИНФ.СЛОВО
 УИ М10 АНАЧ.МАССИВА
 УИА 0
 СЧ 1(М10)
 УИА 3
 ЗП СЛ1   1-АЯ ЯЧ.МАТ.
 УИА 0
 СЧ (М10)
 УИА 3
 ЗП СЛ  0-АЯ ЯЧ.МАТ.
 И П77777
 ВЧОБ Е11   ДЛИНА МАС.<2000
 ПЕ БРОШ НЕТ
 СЧ СЛ1
 ПО БРОШ ШИФР=0
 СЧ ПЛИС
 ПО ПБУФ5
 МОД ИПЗ
 СЧ ШИФРМ
 НТЖ СЛ1 ШИФРЫ СОВПАДАЮТ
 ПЕ БРОШ НЕТ
ПБУФ5 ПБ ТИГР2
УБР48 НТЖ СЛ
 ЗП (М14)
 ПБ УПР2  КЗ-ИДЕМ НА ПРОМЕЖ.ВЫД.
ПБУФ2 СЧИ М1
 ЗП КЗ УПРЯТ.М1
 УИА -'1777'(М1)
ЗПЛ УИА 0
 СЧ 0(М10)
 УИА 3
 ЗП 0(М14)
 СЛИА 1(М10)
 СЛИА 1(М14)
 МОДА ЗПЛ
 ЦИКЛ (М1)
 СЛИА -1024(М14)
 УИА -3(М1)
З141 СЧ '377'(М14)
 СДА 64+8
 СДА 64-8
 ИЛИ П141
 ЗП '377'(М14)
 СЛИА '400'(М14)
 МОДА З141
 ЦИКЛ (М1)
 СЛИА -1024(М14)
 СЧ КЗ
 УИ М1
 СЧ СЛ
 И Е48 КОНЕЦ ФАЙЛА
 МОД ИПЗ
 УИА (М10)
 МОДА БР6
 УИА (3)
 УИА 1(М12)
 ПЕ УБР48
 УИА И10(М12)
 ПБ УПР2 НЕ КЗ
БРОШ СЧ ИПЗ
 УИ М10
 УИА '42'(М12)
 ПБ БР8С
КЗ КОНД В'0'
СЛ КОНД В'0'
СЛ1 КОНД В'0'
ПЛИСТ КОНД П'ЛИСТОМ'
КЛЮЧЛИ КОНД П'КЛЮЧЛИ'
ПЛИС ПАМ 1
КЛЮЧЛ ПАМ 1
Е18П16 КОНД М15В'7'
 ВХОД КЛЮЧЛ
.ПБУФ1 АNОР
АВР СЧ ОТЛ
 ПО ФИКАВР
 МОД ТУС+15
 СЧ ИЗАХВ
 И Е48
 ПО ТИГР
ФИКАВР СЧ (М14)
 ПО ТИГР          НЕТ ВЫДАЧ
 СЧ НУЛЯЧ
 ИЛИ (М14)
 ЗП (М14)
 ЗП 256(М14)
 ЗП 512(М14)
 ЗП 768(М14)
 МОД ИПЗ
 СЧ ШЗНМБ+2
 И Е34П25
 СЛЦ Б5+4
 ПВ ФИЗОБМ(М16)
 СЛИА ТИГР-Т(М15)
 ПБ БР171А
И10 СЛИА 1(М3)         СЧИТ. ОЧЕРЕДН. ИНФ. СЛОВА
 УИА '2000'
 СЧ (М3)
 УИА 3
 ЗП Б5+3
 СДА 64+44
 И П7
 ЗП ОСТ+9         ВИД ПЕЧАТИ
 МОДА БР20А
 УИА (М11)
 СЧ Б5+3
 СДА 64+12
 И П177
 ЗП ОСТ+8         РАССТ. МЕЖДУ СТОЛБЦ.
 СЧ Б5+3
 ПБ ИТМ
АИТМ ЗП ОСТ+10        ЧИСЛО СТОЛБЦ.-1
 СЧ Б5+3
 СДА 64+24
 И П177
 ЗП ОСТ+11        ЧИСЛО СИМВ. В КОНСТАНТЕ
 СЧ Б5+3
 И Е24
 ЗП ОСТ+12        КОНЕЦ ИНФ. СЛОВ
 СЧ Б5+3
 СДА 64+36
 И П177
 ПБ И12
И9 ВЧ Е1
 ЗП ОСТ+10
 СЧ МАТР-3
 СЛЦ ОСТ+8         РАССТ. МЕЖДУ СТОЛБЦАМИ
И12 ЗП МАТР-3        НАЧ. ПОЗИЦ.
 ВЧ МАТР-4
 ПЕ И11А          НАЧ.ПОЗИЦ. МЕНЬШЕ ТЕКУЩЕЙ
 СЧ МАТР-3
 ВЧОБ П177          НОМЕР ПОЗИЦ >177
 ПО И11           НЕТ
 СЧ
 ЗП МАТР-3
И11А МОДА КОНСТР
 ПВ (М5)
И11 СЧ МАТР-3
 ЗП МАТР-4
И16 УИА '2000'        СЧИТ. СОД. ЯЧЕЙКИ ДЛЯ ВЫДАЧИ
 СЧ (М10)
 УИА 3
 ЗП Б5+3
 МОД ОСТ+9
 ПБ И17           УХОД В ЗАВИСИМОСТИ ОТ ВИДА ПЕЧАТИ
И17 УИА -5(М13)
 ПБ БР            БУКВ.-ЦИФР. ПЕЧАТЬ
И17А УИА -1(М11)
 ПБ БР54          КОМАНДЫ
И17Б СЧ ОСТ+11
 ПБ БР51          КОНСТАНТЫ
 МОДА 0
 ПБ ЧИСЛ
 МОДА 0
 ПБ И17
 УИА -1(М11)
 ПБ И17А
 СЧ Е47
 ПБ ПЕЧ4Р
 СЧ Е47
 ПБ ПЕЧ4Р
ЧИСЛ УИА 10(М11)
 И Е41
 ПО И21А
 УИА 11(М11)
И21А СЧ Б5+3
 РЖА '40'
 ВЧАБ
 ЗП Б5+3
 РЖА 4
 УИА (М13)
 ПО И23
 УИА 1(М13)
И21 ЗП Б5+3
 ВЧ =Е'10'
 ПЕ И22
 СЛИА 10(М13)
 СЧ Б5+3
 УМН ВОЛ10
 ПБ И21
ВОЛ10 КОНД В'1755574677547304'
И22 СЧ Б5+3
 ВЧ Ч1Д
 ПО И23
 СЛИА -1(М13)
 СЧ Б5+3
 УМН =Е'10'
 ЗП Б5+3
 ПБ И22
И23 РЖА 3
 МОДА БР5
 ПВ (М5)
 СЧИ М11
 МОДА БР20
 ПВ (М5)
И23Г СЧ ОСТ+11
 УИА (М11)
 СЛИА И20А-И23Г(М5)
 ВЧОБ П5
 ПО И20
 УИ М11
И20 СЧ Б5+3
 СЛ ЭК9
 И П17
 ПБ БР20
И20А СЧ СИМВ
 ИЛИ ЭК9
 РЖА
 ВЧОБ Б5+3
 УМН =Е'10'
 РЖА 3
 ЗП Б5+3
 МОДА И20
 ЦИКЛ (М11)
 СЧ Е5
 МОДА БР20
 ПВ (М5)          НА ЗАНЕС. E
И24А СЧИ М13
 СЛИА И25А-И24А(М5)
 УИА 10(М11)
 СДА 64+7
 ПО И25
 СЧИ М13
 ВЧОБ
 УИ М13
 УИА 11(М11)
СОСТАВ ВНЕШ ИЗАХВ
И25 СЧИ М11
 ПБ БР20
И25А СЛИА -10(М13)
 СЧИ М13
 СДА 64+5
 ПЕ И26
 СЧ Е1
 ПБ И27
И26 СЛИА 10(М13)
 СЧ
И27 МОДА БР20
 ПВ (М5)
 МОДА БР17
 УИА (М5)
 СЧИ М13
 ПБ БР20   КОНЕЦ ЧИС.ВЫД
ПЕЧ4Р ЗП СИМВ+1
 СЧ ОСТ+11
 УИ М11
 УИА 16(М15)
 ПО БР17
 ВЧОБ ПР13
 ПО ОБХОД
 УИА 12(М11)
ОБХОД СЛИ М15(М11)
 СЛИ М15(М11)
 СЛИ М15(М11)
 СЛИ М15(М11)
 СЧ Б5+3
 СДА (М15)
 ЗП Б5+3
СЛУГА СЧ Б5+3
 СДА 64-4
 ЗП Б5+3
 СЧМР 0
 ЗП СИМВ
 ВЧ П12
 ПЕ НОРМА
 УИ М5
 СЧ БУФЕР
СЛЕД МОДА ОКЕЙ
 ПИО (М5)
 СЛИА -1(М5)
 СДА 64+8
 ПБ СЛЕД
ОКЕЙ И П377
 ЗП СИМВ
НОРМА СЧ СИМВ
 МОДА БР20
 ПВ (М5)
 СЛИА -1(М11)
 МОДА СЛУГА
 ПИНО (М11)
 ПБ БР17
БР51 УИ М11           ПЕЧАТЬ В ФОРМЕ КОНСТАНТ
 УИА '20'(М15)
 ПО БР17
 ВЧОБ П17
 ПО БР52
 УИА '20'(М11)
БР52 СЛИ М15(М11)
 СЛИ М15(М11)
 СЛИ М15(М11)
 СЧ Б5+3
 СДА (М15)
 ЗП Б5+3
БР53 СЧ Б5+3
 СДА 64-3
 ЗП Б5+3
 СЧМР
 МОДА БР20
 ПВ (М5)
 СЛИА -1(М11)
 МОДА БР53
 ПИНО (М11)
 ПБ БР17
БР54 И Е44           ПЕЧАТЬ В ВИДЕ КОМАНД
 УИА (М16)
 ПО БР55
 СЛИА 1(М16)
БР55 СЧ Б5+3
 МОДА КОД
 РЗБ (М16)
 МОДА КОД+2
 ИЛИ (М16)
 МОДА БР57
 УИА (М5)
БР56 СДА 64-4
 ЗП Б5+6
 СЧМР
 ЗП СИМВ
 ПБ ББ
БР57 СЧ Б5+6
 ПЕ БР56          ПРОБА КОНЦА КОМАНДЫ
 СЧ Б5+3
 СДА 64-24
 ЗП Б5+3
 МОДА БР54
 ЦИКЛ (М11)         ЯЧЕЙКА ВСЯ
 ПБ БР17
ВЫБСИМ МОДА ВЫБ
 ЦИКЛ (М13)
И40 УИА '2000'
 СЧ (М14)
 УИА 3
 ЗП Б5+3
 ПБ (М17)
ВЫБ1 СЛИА 1(М14)
 УИА -5(М13)
ВЫБ СЧ Б5+3          ВЫДЕЛ. СИМВОЛА
 СДА 64-8
 ЗП Б5+3
 СЧМР
 ЗП СИМВ
 ПБ АЛОЗ1
БР МОДА ВЫБ           БУКВ. ЦИФР. ПЕЧАТЬ
 ПВ (М5)
МЕТКА МОДА БР  ЗА СИМВ
 ЦИКЛ (М13)
 СЧ ОСТ+9
 И Е3
 ПЕ АВИТМ
 МОДА БР17А
 ПИНО (М6)          ЕСЛИ ВЫДАЧА В СТОЛБЦЫ
 ПБ АВИТМ
НЕТЛ СЧ Л(М1)
 ЗП СХ
 СЧ 0
 ЗП Л(М1)
 СЧ МАТР-4
 СЛЦ Е1
 ЗП МАТР-4
 ПБ (М5)
АВИТМ УИА (М6)
 СЧИ М10
 ВЧ Б5+2
 ПО БР171   КНЦ
 СЛИА 1(М10)
 ПБ И16
БР20А ВЧ П140
 ПЕ БР3   ОБЫЧ
 СЛИА -'201'(М15)
 МОДА НПЗ
 ПИО (М15)
 СЛИА 7(М15)
 МОДА БР17
 ПИО (М15)
 СЛИА -'205'(М15)
 МОДА БР17
 ПИО (М15)
 СЛИА '146'(М15)
 МОДА БР17
 ПИО (М15)
 МОДА БР5А
 ПИНО (М6)          ЕСЛИ ВЫДАЧА В СТОЛБЦЫ
 СЛИА '31'(М15)
 МОДА БР11    НОМ.ПОЗ
 ПИО (М15)
 СЛИА 5(М15)
 МОДА БР11
 ПИО (М15)
 СЛИА -1(М15)
 МОДА БР13
 ПИО (М15)         СИМВОЛ ПОВТОРЕНИЯ
 СЛИА -'71'(М15)
 МОДА БР13
 ПИО (М15)
 СЛИА '51'(М15)
 МОДА БР8    ПЕРЕХОД
 ПИО (М15)
 СЛИА '17'(М15)
 МОДА БР8
 ПИО (М15)
 СЛИА -'45'(М15)
 МОДА БР5А
 ПИО (М15)         СИМВОЛ '242' ПРОБЕЛ
 СЛИА 26(М15)
 МОДА Б8
 ПИО (М15)
 СЛИА -64(М15)
 МОДА НЕТЛ
 ПИО (М15)
 СЛИА -25(М15)
 МОДА (М5)
 ПИО (М15)
 СЛИА '176'(М15)
 МОДА (М5)
 ПИО (М15)
 СЛИА -39(М15)
 МОДА БР3
 ПИО (М15)
 СЛИА '212'(М15)
 ПБ БР5А
НПЗ СЧ МАТР-4
 СЛЦ Е1
 ЗП МАТР-4
 ПБ ЛИ
ЗВОН КОНД В'0'
СХ КОНД В'0'
П33 КОНД В'33'
КОН22 КОНД М39В'22'
О КОНД В'26537100173'
КОД КОНД В'560156701673560'
 КОНД В'560156035673560'
 КОНД В'17000074000017'
 КОНД В'17001700000017'
 ЛИТЕР
АЛОЗ1 УИА (М15)
 СЧ СИМВ+1
 ПО ИТМ2
 СЧ СИМВ
 УИ М15
 ПБ (М11)         СИМВОЛ УПП
ИТМ2 СЧИ М11
 ЗП СИМВ+2        УПРЯТЫВАНИЕ
 СЧ СИМВ
 НТЖ П377
 ПЕ МЕТКУ
 СЧ П33
 ЗП СИМВ
 УИ М15
 ПБ (М11)
МЕТКУ УИА -15(М11)
 МОДА ИТАБЛ+15
 СЧ (М11)
ИТМ1 ЗП СИМВ+3
 И П377
 НТЖ СИМВ
 ПО ИВЫХОД        СИМВОЛ НАЙДЕН
 СЛИА 1(М15)
 СЧ СИМВ+3
 СДА 64+8
 ПЕ ИТМ1
 МОДА ИТАБЛ+16
 СЧ (М11)
 МОДА ИТМ1
 ЦИКЛ (М11)
 СЛИА '32'(М15)
 СЧИ М15
 СДА 64+7
 ПЕ МОН
 СЧ ИТАБ
 ПБ ИТМ1
МОН УИА '231'(М15)
ИВЫХОД СЧ СИМВ+2
 УИ М11
 СЧИ М15
 ЗП СИМВ
 ПБ (М11)
АЛОЗ МОДА И40
 ПВ (М17)
 И Е47
 НТЖ Е47
 ЗП СИМВ+1
 ПБ И74А
П45 КОНДС В'45'
П174 КОНД В'174'
УПРЯЧЬ КОНД П'УПРЯЧЬ'
О16П9 КОНД В'-1'М8В'377'
П213 КОНД В'213'
ЧСТ КОНД М47В'1'В'110'
СМ5 КОНД В'0'
БУФЕР КОНД М40В'100'М32В'45'М24В'77'М16В'61'М8В'42'В'40'    FЕDВ
ИТАБЛ КОНК К'001202003'
 КОНК К'000400400'
 КОНК К'032030411'
 КОНК К'002003406'
 КОНК К'060677017'
 КОНК К'021623067'
 КОНК К'022622457'
 КОНК К'032424476'
 КОНК К'061465041'
 КОНК К'031074066'
 КОНК К'142711723'
 КОНК К'112027147'
 КОНК К'152546321'
 КОНК К'143710322'
 КОНК К'143301605'
 КОНК К'101744636'
 КОНК К'111753225'
 КОНК К'100307215'
 КОНК К'151752701'
 КОНК К'151154716'
 КОНК К'102713222'
 КОНК К'153356320'
 КОНК К'102516606'
 КОНК К'102315214'
 КОНК К'022110631'
 КОНК К'103716224'
 КОНК К'061226471'
 КОНК К'030026074'
 КОНД М40В'150'М32В'044'М24В'042'М16В'062'М8В'000'В'065'
 КОНД М40В'137'М32В'075'М24В'135'М16В'134'М8В'063'В'043'
ИТАБ КОНД В'1000007336661140'
Б1 КОНД (128)В'0'
ОТЛ ПАМ 1
Б5 ПАМ 88
дисп99 ВНЕШ ИВ25,П1777,П377,П3,П17,П177
дисп99 ВНЕШ ТБУФ,П77777,ЗОНЫ,Е34П25,Е46Е45,НОММЛ3
дисп99 ВНЕШ ШЗНМБ,П7,ПР13,ЭК9,П140,ТУС,ШИФРМ,АВОС,Ч1Д
дисп99 ВНЕШ ЖДЕТ
дисп99 ВНЕШ ЯЧКОПИ,висп16,Е40П1,Е15П11
дисп99 ВНЕШ ЕЗП,ЗАВР,П77,П5,ПЗ,П12
дисп99 ВНЕШ е48п17,е48п41,е48п42,е48п16
дисп99 внеш ВШГП,ВШГ,ФИЗОБМ
 ЭКВИВ (А,Э64),(И9А,ТИГР),(ИПЗ,Б5+1),(МАТР,Б5+29)
 ЭКВИВ (ОСТ,Б5+76),(СИМВ,Б5+15),(МАТР1,МАТР+35)
 ЭКВИВ (Л,0),(ЧС,1),(ПРОТ,1)
 ВХОД ДЛИНА,И10
*
*       $Log: e64.bs,v $
*       Revision 1.2  1999/01/27 00:24:50  mike
*       e64 and e62 '41' implemented in supervisor.
*
*       Revision 1.1  1998/12/30 02:55:56  mike
*       Initial revision
*
*
 ФИНИШ
КВЧ$$$
ТРН$$$
ЧТКОМП420070
БТМАЛФ
ЗОНМОД430140  0002
КНЦ$$$
*end file
``````
ЕКОНЕЦ
