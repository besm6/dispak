шифр 419999 зс5^
лен 42(2099)^
лен 43(2099-зп)^
еев1а3
*name ekdisp.b6
*bemsh
ВВД$$$
экдисп СТАРТ '12000'
 Е
 М
 Б
 УПОТР экдисп(М7)
 КОНД В'0'          ИПЗ экдисп
 КОНД М41В'3'
 КОНД (21)В'0'
 КОНД В'0'  НЕ ЗАНИМАТЬ
 пам 7
 ПАМ 1     ПРИП
 КОНД В'0'
 КОНД М47В'1'
 ПАМ 6
вход ноп
 уи м1  нзад
 мод тус-1(м1)
 уиа (м2)       ипз
 сч 3(м2)
 сда 64+15
 уи м3  номер эк
 сч 36(м2)
 уи м4  аисп эк
 сч (м2)
 уии м16(м3)
 мода э50               э50
 пио (м16)
 слиа -'12'(м16)        э62
 мода э62
 пио (м16)
нереал ноп
 уиа 3(м16)
выход ноп
 счи м16
 СДА 64-15
 НТЖ 3(М2)
 И Е48П16
 НТЖ 3(М2)
 ЗП 3(М2)
 СЧ 33(М7)
 И =М23В'-1'М47В'1'
 СЛЦ 33(М2)
 ЗП 33(М2)
 сч е48-1-пз(м1)
 уиа висп16(м15)
 пб вшгп
э50 ноп
 уии м16(м4)
 слиа -'104'(м16)        запрос фио по шифру
 мода э50104
 пио (м16)
 слиа -2(м16)
 мода э50106             первое слово своего фио на см
 пио (м16)
 слиа -1(м16)
 мода э50107             второе слово своего фио на см
 пио (м16)
 пб нереал
э62 ноп
 уии м16(м4)
 слиа -'41'(м16)        запрос вых.потока
 мода э6241
 пио (м16)
 пб нереал
хорошо ноп
 уиа 3
 сч  п77777
эквых ноп
 зп (м2)
конец ноп
 уиа 0(м16)
 пб выход
*
э6241 ноп -     эк запроса вых.потока
 сда 64+18
 и п77
 нтж е1
 пе нереал      умеем только ацпу
 сч (м2)
 сда 64+2
 и е15п11
 уи м14         адрес записи потока
 сч (м2)
 и п1777
 зп р1          номер зоны потока
 сч шзнмб+2(м2)
 сда 64+24
 и п1777
 зп р   текущая зона
 вчоб р1
 пе диск
 или 0
 по лист
ко2 ноп
 сч е2
 пб эквых       такой зоны пока нет
диск ноп
 счи м14
 уиа 1027
 зп рмр
 сч р1
 сда 64-24
 слц номмл3
 или е18
 мода хорошо
 уиа (м16)
 пб физур
лист ноп
 счи м1
 и п77
 сда 64-6
 или е6
 пв тбуф(м15)   запрос 40-го листа задачи
 сда 64-8
 уи м13
 уиа -1023(м16)
переп ноп
 мода (м16)
 сч 1023(м13)
 уиа 1024
 мода (м16)
 зп 1023(м14)
 уиа 3
 мода переп
 цикл (м16)
 пб хорошо
*
э50104 ноп -    запрос фио по шифру
 И П77777
 УИ М10
 УИА 1024
 СЧ (М10)
 УИА 3
 СДА 64-32
 ЗП р1 ОГЧЕ В СТ Р
 мода дайфио
 пв (м14)
 сч имяпол
 уиа 1024
 зп (м10)
 уиа 3
 сч имяпол+1
 уиа 1024
 зп 1(м10)
 пб конец
э50106 ноп -    первое слово своего фио на см
 уиа 0(м13)
фиосм ноп
 сч шифрм(м2)
 сда 64+32
 сда 64-32
 ЗП р1 ОГЧЕ В СТ Р
 мода дайфио
 пв (м14)
 мода (м13)
 сч имяпол
 пб эквых
э50107 ноп -    второе слово своего фио на см
 уиа 1(м13)
 пб фиосм
дайфио ноп -    возврат по м14
 СЧ НОММЛ1
 ЗП р
 УИА -3(М5)
 СЧ 0
 ПВ ТБУФ(М15)
 СДА 64-8
 УИ М11
 МОДА ПР3
 ПВ (М6)
 СЛИА 1(М5)
 УИА -511(М12)
 УИА 1(М15)
 СЛИ М15(М11)
ГДЕ СЧ (М15)
 НТЖ р1
 СДА 64+32
 ПО ЕС НАШЛИ ФИО
 СЛИА 2(М15)
 МОДА ГДЕ
 ЦИКЛ (М12)
 МОДА ГДЕ1
 ПИО (М5)
 СЧ Е25
 СЛЦ КАТ
 ПБ ПР30
ГДЕ1 СЧ 0
 зп имяпол
 зп имяпол+1
 ПБ откат
ЕС СЧ -1(М15)
 СДА 64-36
 ЗП р1
 СЧ (М15)
 СДА 64-24
 СДА 64+12
 ИЛИ р1
 ЗП (М15)
 мода имяпол
 уиа (м17)
 УИА -1(М16)
Э104А МОДА (М15)
 СЧ (М16)
 РЗБ МАСК
 СЛЦ ЗП
 ЗП (М17)
 МОДА Э104А
 ЦИКЛ (М16)
откат ноп
 СЧ КАТ
 ПВ ТБУФ(М15) ОТДАТЬ ЛИСТ
 пб (м14)
ПР3 СДА 64-2
 ИЛИ р
 СЛЦ кусшиф
ПР30 ЗП КАТ
 ПВ ФИЗОБМ(М16) ЧТЕН.КАТАЛ.
 УИА 3
 ПБ (М6)
*
р пам 1
р1 пам 1
кат пам 1
имяпол пам 2
кусшиф КОНД М24В'440'М17В'1'
МАСК КОНД В'1763747717637477'
ЗП КОНД П',,,,,,'
*
дисп99 внеш е48п16,пз,висп16,вшгп,тус,п77,е15п11
дисп99 внеш п1777,шзнмб,рмр,номмл3,физур,тбуф,физобм
дисп99 внеш п77777,номмл1,шифрм
*
*       $Log: ekdisp.bs,v $
*       Revision 1.3  1999/02/09 01:27:04  mike
*       added e50 '104'.
*
*       Revision 1.2  1999/02/02 22:30:06  mike
*       Bugfix: bad nzad for tbuf.
*
*       Revision 1.1  1999/01/27 00:24:50  mike
*       Initial revision
*
*
*
 ФИНИШ
КВЧ$$$
ТРН$$$
ЧТКОМП420070
БТМАЛФ
ЗОНМОД430147  0001
КНЦ$$$
*end file
``````
ЕКОНЕЦ
